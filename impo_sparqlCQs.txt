ImPO Competency Questions and SPARQL-OWL queries


1. For each sample extract the corresponding peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?sample_id ?peptide_sequence WHERE{
?sample imnp:has_id ?sample_id .
?mass_spec imnp:has_input ?sample .
?mass_spec imnp:has_output ?spectrum .
?spec_pep_id imnp:has_source ?spectrum .
?spec_pep_id imnp:has_target ?peptide .
?peptide imnp:has_sequence ?peptide_sequence .
}


2. For each HLA type extract the corresponding peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?hla_class?peptide_sequence WHERE{
        ?hla imnp:has_class ?hla_class . 
           ?motif_hla_assign imnp:has_target ?hla .
            ?motif_hla_assign imnp:has_source ?motif .
            ?motif imnp:has_part ?peptide .
            ?peptide imnp:has_sequence ?peptide_sequence .
}


3. For each Genomic Hotspot extract the corresponding peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?gh_id ?peptide_sequence WHERE{
         ?peptide imnp:has_sequence ?peptide_sequence .
            ?peptide imnp:part_of ?gh .
        ?gh imnp:has_id ?gh_id .
}


4. For each sample extract the MS instrument.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?sample_id ?mass_spec_instrument WHERE{ 
        ?sample imnp:has_id ?sample_id .
          ?mass_spec imnp:has_input ?sample .
          ?mass_spec imnp:has_instrument ?mass_spec_instrument .
}
5. For each protein extract the peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?protein_id ?peptide_sequence WHERE{
        ?protein imnp:has_id ?protein_id .
          ?ppa imnp:has_target ?protein .
          ?ppa imnp:has_source ?peptide .
          ?peptide imnp:has_sequence ?peptide_sequence .
}


6. For each gene extract the peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?gene_id ?peptide_sequence WHERE{
    ?gene imnp:has_id ?gene_id .
    ?transcript imnp:product_of ?gene .
    ?protein imnp:product_of ?transcript .
    ?ppa imnp:has_target ?protein .
    ?peptide a imnp:peptide .
    ?ppa imnp:has_source ?peptide .
    ?peptide imnp:has_sequence ?peptide_sequence .
}


7. For each PTM extract the position in the corresponding peptide.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?ptm_name ?awptm_position ?peptide_sequence WHERE{
    ?ptm imnp:has_name ?ptm_name .
    ?assoc_with_ptm imnp:has_target ?ptm .
    ?assoc_with_ptm imnp:has_source ?mass_shift .
    ?assoc_with_ptm imnp:has_position ?awptm_position .
    ?mass_shift imnp:occurs_in ?peptide .
    ?peptide imnp:has_sequence ?peptide_sequence .
}


8. Extract peptides that are shared across at least 10 non disease free samples.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?peptide_sequence WHERE{
    {
        SELECT ?peptide_sequence ?sample WHERE {
            FILTER EXISTS{?sample imnp:has_cancer_type ?cancer .}        
            {
                SELECT ?peptide_sequence (count(distinct ?sample) as ?count_sample) WHERE {
                    ?sample a imnp:sample .
                    ?mass_spec imnp:has_input ?sample .
                    ?mass_spec imnp:has_output ?spectrum .
                    ?spec_pep_id imnp:has_source ?spectrum .
                    ?spec_pep_id imnp:has_target ?peptide .
                    ?peptide imnp:has_sequence ?peptide_sequence .
                }        
                GROUP BY ?peptide_sequence
                HAVING (?count_sample >= 10) 
            }
        }
     }
}


9. For each peptide, extract all associated mutations and their genomic coordinates.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?peptide_sequence ?geno_mut_id ?geno_mut_start_position ?geno_mut_end_position ?geno_mut_chromosome_number WHERE{
    ?peptide imnp:has_sequence ?peptide_sequence .
    ?aa_subst imnp:occurs_in ?peptide .
    ?aa_subst imnp:has_cause ?genomic_mut .
    ?genomic_mut imnp:has_cosmic_id ?geno_mut_id .
    ?genomic_mut imnp:has_start_position ?geno_mut_start_position .
    ?genomic_mut imnp:has_end_position ?geno_mut_end_position .
    ?genomic_mut imnp:has_chromosome_number ?geno_mut_chromosome_number .    
}


10. For each Genomic Hotspot, extract all associated mutations and their genomic coordinates.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
SELECT DISTINCT ?gh_id ?geno_mut_id ?geno_mut_start ?geno_mut_end ?geno_mut_chr WHERE{
    ?gh a imnp:genomic_hotspot .
    ?gh imnp:has_id ?gh_id .
    ?genomic_mut imnp:occurs_in ?gh .
    ?genomic_mut imnp:has_cosmic_id ?geno_mut_id .
    ?genomic_mut imnp:has_start_position ?geno_mut_start .
    ?genomic_mut imnp:has_end_position ?geno_mut_end .
    ?genomic_mut imnp:has_chromosome_number ?geno_mut_chr .
}


11. For each cancer, extract all associated mutations and proteins.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
SELECT DISTINCT ?cancer ?geno_mut_id ?has_prot_id WHERE{
        ?cancer_type a owl:Class .
        ?cancer_type rdfs:label ?cancer.
        ?cancer_ind a ?cancer_type .
        ?gh imnp:has_role ?cancer_ind .
        ?gh imnp:has_part ?genomic_mut .
        ?genomic_mut imnp:has_cosmic_id ?geno_mut_id .
        ?aa_subst imnp:has_cause ?genomic_mut .
        ?aa_subst imnp:occurs_in ?protein .
        ?protein imnp:has_id ?has_prot_id .
}


12. For each cancer, extract all associated mutations and peptides.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?cancer ?geno_mut_id ?peptide_sequence WHERE{
        ?cancer_type a owl:Class .
        ?cancer_type rdfs:label ?cancer.
        ?cancer_ind a ?cancer_type .
        ?gh imnp:has_role ?cancer_ind .
        ?gh imnp:has_part ?genomic_mut .
        ?genomic_mut imnp:has_cosmic_id ?geno_mut_id .
        ?aa_subst imnp:has_cause ?genomic_mut .
        ?aa_subst imnp:occurs_in ?peptide . 
        ?peptide imnp:has_sequence ?peptide_sequence .
}


13. For each cancer, extract all associated mutations and PSMs.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?cancer ?geno_mut_id ?spectum_id WHERE{
        ?genomic_mut imnp:has_cosmic_id ?geno_mut_id . 
        ?aa_subst imnp:has_cause ?genomic_mut .
        ?aa_subst imnp:occurs_in ?peptide .
        ?spec_pep_id imnp:has_target ?peptide .
        ?spec_pep_id imnp:has_source ?spectrum .
        ?spectrum imnp:has_id ?spectum_id .
        ?mass_spec imnp:has_output ?spectrum .
        ?mass_spec imnp:has_input ?sample .
        ?sample imnp:has_cancer_type ?cancer_ind .
        ?cancer_type a owl:Class .
        ?cancer_type rdfs:label ?cancer.
        ?cancer_ind a ?cancer_type .
}


14. For each cancer, extract all associated mutations and PTMs.
PREFIX imnp: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?cancer ?geno_mut_id ?ptm_name WHERE{
        ?cancer_type a owl:Class .
        ?cancer_type rdfs:label ?cancer.
        ?cancer_ind a ?cancer_type .
        ?gh imnp:has_role ?cancer_ind .
        ?gh imnp:has_part ?genomic_mut .
        ?genomic_mut imnp:has_cosmic_id ?geno_mut_id .
        ?aa_subst imnp:has_cause ?genomic_mut .
        ?aa_subst imnp:occurs_in ?peptide .
        ?mass_shift imnp:occurs_in ?peptide .
        ?assoc_with_ptm imnp:has_source ?mass_shift .
        ?assoc_with_ptm imnp:has_target ?ptm . 
        ?ptm imnp:has_name ?ptm_name .
}


15. For each cancer, extract all associated mutations and Genomic Hotspots. 
PREFIX imnp: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?cancer ?geno_mut_id ?gh_id WHERE{
        ?cancer_type a owl:Class .
        ?cancer_type rdfs:label ?cancer.
        ?cancer_ind a ?cancer_type .
        ?gh imnp:has_role ?cancer_ind .
        ?gh imnp:has_part ?genomic_mut .
        ?gh imnp:has_id ?gh_id .
        ?genomic_mut imnp:has_cosmic_id ?geno_mut_id . 
}